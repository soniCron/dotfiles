zmodload -i zsh/parameter || return 1

setopt promptsubst

prompt_daniel_help () {
    setopt localoptions nocshnullcmd noshnullcmd
    [[ $ZSH_VERSION < 4.2.2 ]] &&
	print 'Requires ZSH_VERSION 4.2.2'$'\n'
    <<-\EOF
	This prompt gives the effect of left and right prompts on the upper
	line of a two-line prompt.  It also illustrates including history
	text in the prompt.  The lower line is initialized from the last
	(or only) line of whatever prompt was previously in effect.

	    prompt daniel [on|off] [color...]

	You may provide up to five colors, though only three colors (red,
	blue, and the default foreground) are used if no arguments are
	given.  The defaults look best on a light background.

	The "off" token temporarily disables the theme; "on" restores it.
	No background colors or hardwired cursor motion escapes are used,
	and it is not necessary to setopt promptsubst.
	EOF
    [[ $(read -sek 1 "?${(%):-%S[press return]%s}") == [Qq] ]] &&
	print -nP '\r%E' && return
    <<-\EOF

	The "upper left prompt" looks like:
	    machine [last command] /current/working/dir
	The first three color arguments apply to these components.  The
	last command is shown in standout mode if the exit status was
	nonzero, or underlined if the job was stopped.

	If the last command is too wide to fit without truncating the
	current directory, it is put on a middle line by itself.  The
	current	directory uses %~, so namedir abbreviation applies.

	The "upper right prompt" looks like:
	    date time
	The fourth color is used for the date, and the first again for the
	time.  As with RPS1, first the date and then the time disappear as
	the upper left prompt grows too wide.  The clock is not live; it
	changes only after each command, as does the rest of the prompt.
	EOF
    [[ $(read -sek 1 "?${(%):-%S[press return]%s}") == [Qq] ]] &&
	print -nP '\r%E' && return
    <<-\EOF

	When RPS1 (RPROMPT) is set before this prompt is selected and a
	fifth color is specified, that color is turned on before RPS1 is
	displayed and reset after it.  Other color changes within RPS1, if
	any, remain in effect.  This also applies to RPS2 (RPROMPT2).
	If a fifth color is specified and there is no RPS2, PS2 (PROMPT2)
	is colored and moved to RPS2.  Changes to RPS1/RPS2 are currently
	not reverted when the theme is switched off.  These work best with
	the TRANSIENT_RPROMPT option, which must be set separately.

	This theme hijacks psvar[7] through [9] to avoid having to reset
	the entire PS1 string on every command.  It uses TRAPWINCH to set
	the position of the upper right prompt on a window resize, so the
	prompt may not match the window width until the next command.

	When setopt nopromptcr is in effect, an ANSI terminal protocol
	exchange is attempted in order to determine the current cursor
	column, and the width of the upper prompt is adjusted accordingly.
	If your terminal is not ANSI compliant, this may cause unexpected
	behavior, and in any case it may consume typeahead.  (Recommended
	workaround is to setopt promptcr.)
	EOF
    [[ $(read -sek 1 "?${(%):-%S[done]%s}") == $'\n' ]] ||
	print -nP '\n%E'
}

integer -g PSCOL=1
typeset -g PSCMD=

prompt_daniel_preexec () {
    setopt localoptions noxtrace noshwordsplit noksharrays unset
    local -a cmd; cmd=( ${(z)3} )
    if [[ $cmd[1] = fg ]]
    then
	shift cmd
	cmd[1]=${cmd[1]:-%+}
    fi
    if [[ $#cmd -eq 1 && $cmd[1] = %* ]]
    then
	PSCMD=$jobtexts[$cmd[1]]
    elif [[ -o autoresume && -n $jobtexts[%?$2] ]]
    then
	PSCMD=$jobtexts[%?$2]
    else
	# Use history text to avoid alias expansion
	PSCMD=$history[$HISTCMD]
    fi
    return 0
}

prompt_daniel_precmd () {
    setopt localoptions noxtrace noksharrays unset
    local zero='%([BSUbfksu]|[FB]{*})' escape colno lineno



    : "${PSCMD:=$history[$[HISTCMD-1]]}"	# Default to history text

    # Using psvar here protects against unwanted promptsubst expansions.

  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_vcs_colors[1]}%b%f%u%c${_prompt_vcs_colors[4]}●%f"
  else
    branch_format="${_prompt_vcs_colors[1]}%b%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

    psvar[7]="$PSCMD"
    psvar[8]=''		# No padding until we compute it
    psvar[9]=()

    # Reset the truncation widths for upcoming computations
    ((PSCOL == 1)) || { PSCOL=1 ; prompt_daniel_ps1 }
    if [[ -o promptcr ]]
    then
	# Emulate the 4.3.x promptsp option if it isn't available
	eval '[[ -o promptsp ]] 2>/dev/null' ||
	    print -nP "${(l.COLUMNS.. .)}\e[s${(pl.COLUMNS..\b.)}%E\e[u" >$TTY
    else IFS='[;' read -s -d R escape\?$'\e[6n' lineno PSCOL <$TTY
    fi
    ((PSCOL == 1)) || prompt_daniel_ps1
    ((colno = COLUMNS-PSCOL))

    # Compute the size of the upper left prompt and set psvar[9] if needed.
    ((${#${(f)${(%%)${(S)PS1//$~zero/}}}[1]} > colno)) && psvar[9]=''

    # Compute and set the padding between upper left and right prompts.
    (((colno -= ${#${(f)${(%%)${(S)PS1//$~zero/}}}[1]}) > 0)) &&
	psvar[8]=${(l:colno:: :)}



}

prompt_daniel_ps1 () {
    setopt localoptions noxtrace noksharrays

    local -ah ps1
    local -h host hist1 hist2 dir space date time rs="%b%f%k"
    local -h eon="%(?.[.%20(?.[%U.%S[))" eoff="%(?.].%20(?.%u].]%s))"

    # Set up the components of the upper line
    host="%{$fg[%m]%}%m$rs"
    hist1="%9(v. . %{$fg[%h]%}$eon%7v$eoff$rs )"
    hist2=$'%9(v.\n'"%{$fg[%h]%}$eon%7v$eoff$rs.)"
    dir="%{$fg[%~] %}%8~$rs"
    space=%8v
    date="%{$fg[%D]%}%W$rs"	# Prefer "%{$fg[%D]%}%W$rs" in the USA?
    time="%{$fg[%@]%}%@$rs"

    # This is just a tad silly ...
    [[ $prompt_theme[1] = oliver ]] && PS1='[%h%0(?..:%?)]%# ' ||
	PS1=${PS1//$prompt_newline/$'\n'}


    # Assemble the new prompt
    ps1=( ${(f)PS1} )		# Split the existing prompt at newlines
    ps1=(
	"%$[COLUMNS-PSCOL]>..>"	# Begin truncation (upper left prompt)
	"$host"
	"$dir"
	"$hist1"		# Empty when too wide for one line
	"%<<"			# End truncation (end upper left prompt)
	"$space"		# Pad line to upper right position
	"%$[COLUMNS-PSCOL-15](l. . $date)"
	"%$[COLUMNS-PSCOL-7](l.. $time)"
	"$hist2"		# Empty when $hist1 is not empty
	$'\n'
	$ps1[-1]		# Keep last line of the existing prompt
	)
    PS1="${(j::)ps1}"
}

prompt_daniel_winch () {
    setopt localoptions nolocaltraps noksharrays unset

    # Delete ourself from TRAPWINCH if not using our precmd insert.
    [[ $precmd_functions = *prompt_daniel_precmd* ]] && prompt_daniel_ps1 ||
	functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_daniel_winch}"
}

prompt_daniel_setup () {
    setopt localoptions nolocaltraps noksharrays unset
    typeset -gA fg

	autoload -Uz vcs_info

    # A few extra niceties ...
    repeat 1; do case "$1:l" in
      (off|disable)
	add-zsh-hook -D precmd "prompt_*_precmd"
	add-zsh-hook -D preexec "prompt_*_preexec"
	functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_daniel_winch}"
	[[ $prompt_theme[1] = daniel ]] && PS1=${${(f)PS1}[-1]}
	return 1
	;;
      (on|enable)
	shift
	[[ $prompt_theme[1] = daniel ]] && break
	;&
      (*)
	# Use the fg assoc to hold our selected colors ...
	# This used to be provided by the function colors, but is now
	# set directly from here.  There should be no clash if both
	# are in use.
	# fg[%m]="%F{${1:-red}}"
	if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
		MACHINE_COLOR=$(echo "ibase=16; $(hostname | md5sum | cut -c1-2 | tr \[a-z\] \[A-Z\])" | bc)
	else
		MACHINE_COLOR=$(echo "ibase=16; $(hostname | md5sum | cut -c1 | tr \[a-z\] \[A-Z\])" | bc)
	fi

	fg[%m]="%F{${5:-$MACHINE_COLOR}}"
	fg[%~]="%F{${1:-default}}"
	fg[%h]="%F{${2:-blue}}"
	fg[%D]="%F{${3:-default}}"
	fg[%@]="%F{${4:-$MACHINE_COLOR}}"
	;;
    esac; done

	_prompt_vcs_colors=(
	  "%F{cyan}"
	  "%F{yellow}"
	  "%F{magenta}"
	  "%F{red}"
	  "%F{green}"
	)

	  # Formats:
	  #   %b - branchname
	  #   %u - unstagedstr (see below)
	  #   %c - stagedstr (see below)
	  #   %a - action (e.g. rebase-i)
	  #   %R - repository path
	  #   %S - path in the repository
	  local branch_format="(${_prompt_vcs_colors[1]}%b%f%u%c)"
	  local action_format="(${_prompt_vcs_colors[5]}%a%f)"
	  local unstaged_format="${_prompt_vcs_colors[2]}●%f"
	  local staged_format="${_prompt_vcs_colors[5]}●%f"

	  # Set vcs_info parameters.
	  zstyle ':vcs_info:*' enable git
	  # other flavors: bzr git hg svn
	  zstyle ':vcs_info:*:prompt:*' check-for-changes true
	  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
	  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
	  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
	  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
	  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

    prompt_daniel_ps1

    if (($# > 4))
    then
	# No RPS1 by default because prompt_off_setup doesn't fix it.
	if (($#RPS1))
	then
	    RPS1="%F{$5}$RPS1%f"
	fi
	# RPS2 is less obvious so don't mind that it's not restored.
	if (($#RPS2))
	then
	    RPS2="%F{$5}$RPS2%f"
	else
	    RPS2="%F{$5}<${${PS2//\%_/%^}%> }%f"
	    PS2=''
	fi
    fi
    # Paste our special commands into precmd and TRAPWINCH
    
    add-zsh-hook precmd prompt_daniel_precmd
    add-zsh-hook preexec prompt_daniel_preexec
    functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_daniel_winch}
	prompt_daniel_winch"

    return 0
}

prompt_daniel_preview () {
    local +h PS1='%# '
    prompt_preview_theme daniel "$@"
}

[[ -o kshautoload ]] || prompt_daniel_setup "$@"
